Whitespace = " " | "\t" | "\n" | "\r" ;
_ = { Whitespace } ;
__ = Whitespace, _ ;

Digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
Lowercase = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o"
  | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z";
Uppercase = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O"
  | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z";
HexCharacter = Digit | "a" | "b" | "c" | "d" | "e" | "f" | "A" | "B" | "C" | "D" | "E" | "F" ;
Character = ? any printable character ? ;
EscapeSequence = "\", (
  ("u", "{", HexCharacter, { HexCharacter }, "}")
  | ("x", 2 * HexCharacter)
  | Character - ("u" | "x")
) ;
Letter = Lowercase | Uppercase ;

(* LITERALS & EXPRESSIONS *)

Integer = Digit, { Digit } ;
Float = [ Integer ], ".", Integer ;
String = '"', { Character - ("\" | '"') | EscapeSequence }, '"' ;

Identifier = Letter, { Letter | Digit | "_" } ;

Literal = Integer | Float | String ;

Expression = Term, { _, ("+" | "-"), _, Term } ;
Term = Factor, { _, ("*" | "/"), _, Factor } ;
Factor = Literal | Identifier | FunctionCall | "(", _, Expression, _, ")" ;

FunctionCall = Identifier, _, "(", _, FunctionCallArguments, _, ")" ;
FunctionCallArguments = [ FunctionCallArgument, { _, ",", _, FunctionCallArgument } ] ;
FunctionCallArgument = VariableAssignment | Identifier ;

(* STATEMENTS *)

Statement = _, (VariableDeclaration | Expression | FunctionDeclaration), _, ";", _ ;
Program = { Statement } ;

VariableDeclaration = "let", __, Identifier, _, "=", _, Expression ;
VariableAssignment = Identifier, _, "=", _, Expression ;

FunctionDeclaration = "fn", __, Identifier, _,
  "(", _, FunctionDeclarationParameters, _, ")",
  "{", _, Program, [ ReturnStatement ], _, "}" ;
FunctionDeclarationParameters = [ Identifier, { _, ",", _, Identifier } ] ;
ReturnStatement = "return", __, Expression, _ ;
